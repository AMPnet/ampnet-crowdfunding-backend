buildscript {
    ext {
        kotlinVersion = '1.3.11'
        springBootVersion = '2.1.0.RELEASE'
        junitVersion = '5.3.2'
        grpcVersion = '1.17.0'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.7")
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'jacoco'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

group = 'com.ampnet'
version = '0.0.1-SNAPSHOT'
description = "crowdfunding-backend"

bootJar {
    baseName = 'crowdfunding-backend'
    version =  '0.0.1'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

configurations {
    ktlint
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.2"
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile('org.springframework.boot:spring-boot-starter-mail')
    kapt "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    compile("org.springframework.social:spring-social-facebook:2.0.3.RELEASE")
    compile('org.springframework.social:spring-social-google:1.0.0.RC1')

    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7')
    compile('io.jsonwebtoken:jjwt-jackson:0.10.5')
    runtime('io.jsonwebtoken:jjwt-impl:0.10.5')
    compile('org.flywaydb:flyway-core:5.2.3')
    compile('org.postgresql:postgresql:42.2.5')
    compile('io.github.microutils:kotlin-logging:1.6.22')

    compile 'net.devh:grpc-client-spring-boot-starter:2.1.0.RELEASE'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    testCompile("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testCompile("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testCompile('org.mockito:mockito-core:2.23.4')
    testCompile('org.subethamail:subethasmtp:3.1.7')

    ktlint "com.github.shyiko:ktlint:0.29.0"
}
compileJava.dependsOn(processResources)

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.6.1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

asciidoctor {
    dependsOn test
    attributes 'snippets': file('build/generated-snippets')
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
    sourceDirectories = files(["${project.projectDir}/src/main/kotlin"])
    classDirectories = fileTree(dir: "${buildDir}/classes/kotlin/main", excludes: ['**/model/**'])
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}
jacocoTestCoverageVerification.mustRunAfter jacocoTestReport

task qualityCheck(type: GradleBuild) {
    tasks = ['ktlintFormat', 'jacocoTestReport', 'jacocoTestCoverageVerification']
}
